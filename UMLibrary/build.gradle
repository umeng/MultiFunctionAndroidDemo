apply plugin: 'com.android.library'

ArrayList<String> getShareDir(String name) {
    ArrayList<File> tmp = new ArrayList<File>()
    File[] files = (new File(rootDir, "share")).listFiles()
    if (files != null) {
        tmp.addAll(files)
    }
    ArrayList<String> dirs = new ArrayList<String>()
    while (tmp.size() > 0) {
        File file = tmp.remove(0)
        if (file.isDirectory() && name == file.getName()) {
            dirs.add(file.getPath())
        } else if (file.isDirectory()) {
            files = file.listFiles()
            if (files != null) {
                tmp.addAll(files)
            }
        }
    }
    return dirs
}

android {
    compileSdkVersion 33

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 33

        sourceSets {
            main {
                ArrayList<String> resSrcList = getShareDir("res")
                resSrcList.add("src/main/res")
                res.srcDirs = resSrcList
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    api fileTree(dir: 'libs', include: ['*.jar', '*.aar'])
    ArrayList<String> share = getShareDir("libs")
    for (String item : share) {
        api fileTree(dir: "${item}", include: ['*.jar', '*.aar'])
    }
}